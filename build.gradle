plugins {
    id 'fabric-loom' version '0.2.4-SNAPSHOT'
    id 'net.minecrell.licenser' version '0.4.1'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
}

archivesBaseName = "LibMultiPart"
version = "0.1.0+build.96"

license {
    header = project.file('misc/LICENSE_HEADER.txt');
    newLine = false;
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:1.14.2"
    mappings "net.fabricmc:yarn:1.14.2+build.2"
    modCompile "net.fabricmc:fabric-loader:0.4.8+build.154"

    //Fabric api
    modCompile "net.fabricmc.fabric-api:fabric-api:0.3.0+build.175"

    modCompile "alexiil.mc.lib:libblockattributes-core:0.4.7"
    modCompile ("alexiil.mc.lib:libnetworkstack:0.1.1+build.34") { transitive = false }

    // Misc
    compile "com.google.code.findbugs:jsr305:3.0.1"
    testCompile "junit:junit:4.12"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

compileJava {
    options.compilerArgs << "-Xmaxerrs" << "2000"
    options.compilerArgs << "-Xmaxwarns" << "2"
    options.compilerArgs << "-Xlint:all"
    options.compilerArgs << "-Xdiags:verbose"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
    publications {
       publishRemapJar(MavenPublication) {
            groupId "alexiil.mc.lib"
            artifactId "libmultipart"
            version project.version

            artifact (jar) {
                classifier ""
                builtBy remapJar
            }
            pom {
                developers { developer { id = "AlexIIL" } }
                dependencies { clear(); }
            }
        }
        publishRemapSrcJar(MavenPublication) {
            groupId "alexiil.mc.lib"
            artifactId "libmultipart"
            version project.version

            artifact (sourcesJar) {
                classifier "sources"
                builtBy remapSourcesJar
            }
            pom {
                developers { developer { id = "AlexIIL" } }
                dependencies { clear(); }
            }
        }
    }
}
