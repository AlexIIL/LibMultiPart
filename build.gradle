plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'net.minecrell.licenser' version '0.4.1'
}

apply plugin: 'maven-publish' // for uploading to a maven repo

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { name="BuildCraft"; url="https://mod-buildcraft.com/maven" }
}

archivesBaseName = "LibMultiPart"
version = "0.4.3"

license {
    header = project.file('misc/LICENSE_HEADER.txt');
    newLine = false;
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:1.16.4"
    mappings "net.fabricmc:yarn:1.16.4+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.10.6+build.214"

    // Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.25.1+build.416-1.16"

    modApi "alexiil.mc.lib:libblockattributes-core:$libblockattributes_version"
    modApi "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"

    include "alexiil.mc.lib:libblockattributes-core:$libblockattributes_version"
    include "alexiil.mc.lib:libnetworkstack-base:$libnetworkstack_version"

    // Misc
    compile "com.google.code.findbugs:jsr305:3.0.1"
    testCompile "junit:junit:4.12"
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            // This is unfinished - so we won't build it quite yet
            exclude "**/PartTags.java"
        }
    }
}

compileJava {
    options.compilerArgs << "-Xmaxerrs" << "2000"
    options.compilerArgs << "-Xmaxwarns" << "2"
    options.compilerArgs << "-Xlint:all"
    options.compilerArgs << "-Xdiags:verbose"
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            url System.getenv("MAVEN_DIR") ?: "$projectDir/build/maven"
        }
    }
}

// #####################
//
// Extra jar section
//
// #####################

apply from: "extra_jar_def.gradle"

// Variables (that should be changed)
ext.mainName = "libmultipart"
ext.mavenGroupId = "alexiil.mc.lib";

ext.extra_jar_def__optimised_compression = true;
ext.extra_jar_def__decompress_external_included = true;
ext.extra_jar_def__common_manifest.put(null, ['Sealed': 'true']);

ext.extra_jar_def__fabric_mod_json_replacements = [
  ["\$version", project.version],
  ["\$libblockattributes_version", "$libblockattributes_version"],
  ["\$libnetworkstack_version", "$libnetworkstack_version"]
];

def allIncludes = [
    "assets/libmultipart/icon.png"
];

def jarDeps = [ 
    ["alexiil.mc.lib", "libblockattributes-core", "$libblockattributes_version"],
    ["alexiil.mc.lib", "libnetworkstack-base", "$libnetworkstack_version"]
];

generateJar("base", ["**"], [], true);
generateJar("all", allIncludes, [], false, [], ["base"], [], jarDeps);
// Don't generate a devonly fatjar - as there's only 1 module.

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
